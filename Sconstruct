# :mode=python:

# py3
from configparser import RawConfigParser
import os.path
import sys

EnsureSConsVersion(3,0,0)

#-----------------------------------------------------------------------------
# scons switches

AddOption(
    "--coverage",
    action = "store_true",
    default = False,
    help = "Execute unittests and produce html coverage report"
)

AddOption(
    "--test",
    action = "store_true",
    default = False,
    help = "Build and execute unittests"
)

AddOption(
    "--verbose",
    action = "store_true",
    default = False,
    help = "Build in verbose mode"
)

if GetOption("test"):

    AddOption(
        "-a",
        "--abort",
        action = "store_true",
        default = False,
        help = "abort at first failure",
    )


#-----------------------------------------------------------------------------
# default configureation

if sys.platform.startswith("linux"):
    default_cfg = dict(
        unittest_env = dict(
            CXX = 'g++',
            CPPPATH = ['external', 'src'],
            CXXFLAGS = ['-std=c++14', '-Wall', '-g', '-O2'],
        ),

        cog_env = dict(),
    )

elif sys.platform in ['win32', 'win64']:

    default_cfg = dict(
        unittest_env = dict(
            CXX = 'cl.exe',
            CPPPATH = ['external', 'src'],
            CXXFLAGS = ['/std:c++14', '/Wall', '/O2', '/permissive-'] + [
            '/IGNORE:'
                '4625,' # copy constructor was implicitly defined as deleted
                '4626,' # assignment operator was implicitly defined as deleted
                '4820,' # '4' bytes padding added after data member
                '5026,' # move constructor was implicitly defined as deleted
                '5027,' # move assignment operator was implicitly defined as deleted
            ]
        ),

        cog_env = dict(),
    )

else:
    raise RuntimeError('Unsupported platform %s' % sys.platform)


#-----------------------------------------------------------------------------
# detect site config

site_cfg = File("site.cfg").path

if not os.path.isfile(site_cfg):

    cfg_out = RawConfigParser()

    for s in default_cfg.keys():

        cfg_out.add_section(s)

        data = default_cfg[s]

        for k,v in data.items():

            cfg_out.set(s, k, repr(v))

    with open(site_cfg, 'w') as fd:
        cfg_out.write(fd)
        print("Wrote default site configuration")
        print("    out: %s" % site_cfg)


#-----------------------------------------------------------------------------
# read in site configuration

print("Reading site configuration")
print("    in: %s" % site_cfg)

site_config = RawConfigParser()

with open(site_cfg, 'r') as fd:
    site_config.readfp(fd)


#-----------------------------------------------------------------------------
# create build environments

print("Creating unittest build environment")

unittest_env = Environment()

for k,v in site_config.items('unittest_env'):

    k = k.upper()
    v = eval(v)

    print('    %10s = %s' % (k, repr(v)))

    unittest_env[k] = v

cog_env = Environment()

for k,v in site_config.items('cog_env'):

    k = k.upper()
    v = eval(v)

    print('    %10s = %s' % (k, repr(v)))

    cog_env[k] = v

cog_env.Tool("Cog")

Export("cog_env")


#-----------------------------------------------------------------------------
# import .scons scripts

SConscript("src/numcpp/numcpp.scons")

#-----------------------------------------------------------------------------
# handle coverage

do_coverage = GetOption("coverage")
do_test = GetOption("test")

if do_coverage:

    assert "g++" in unittest_env['CXX'], "coverage only suported for g++ compilers"

    print("Adding coverage flags")

    unittest_env.AppendUnique(
        CXXFLAGS = ['-g', '-O0', '--coverage'],
        LINKFLAGS = ['--coverage'],
        PATH = os.environ['PATH'],
    )

    for k in ['CXXFLAGS', 'LINKFLAGS']:
        v = unittest_env[k]
        print('    %10s = %s' % (k, repr(v)))

#-----------------------------------------------------------------------------
# Does the c++ compiler work?

conf = Configure(unittest_env)

# c++ compiler

if not conf.CheckCXX():
    import sys
    raise RuntimeError('numcpp requires a working c++ compiler')

# c++14 support

sys.stdout.write("Checking for c++14 support ...")

tmp = conf.TryCompile(
    """
    int x = 0b0010;
    """,
    '.cpp'
)

if tmp:
    print(" yes")

else:
    print(" no")
    raise RuntimeError("c++14 support is required")

conf.Finish()


#-----------------------------------------------------------------------------
# building unittests

src = Glob('src/numcpp/tests/*.cpp')
src.append(File('src/executables/unittests.cpp'))

ut = unittest_env.Program('src/executables/unittests', src)[0]

unittest_env.Default(ut)


#-----------------------------------------------------------------------------
# building demo program
if not do_coverage and not do_test:

    for prog in ['demo', 'timing']:
        p = unittest_env.Program(prog, "src/executables/{}.cpp".format(prog))
        unittest_env.Default(p)

#-----------------------------------------------------------------------------
# extra cleaning

if GetOption("clean"):

    for f in src:

        base = os.path.splitext(f.path)[0]

        unittest_env.Clean(ut, base + ".gcda")
        unittest_env.Clean(ut, base + ".gcno")
        unittest_env.Clean(ut, 'tmp1.info')
        unittest_env.Clean(ut, 'tmp2.info')
        unittest_env.Clean(ut, 'tmp3.info')
        unittest_env.Clean(ut, 'coverage.info')
        unittest_env.Clean(ut, 'coverage')

#-----------------------------------------------------------------------------
# execute unittest

if do_test:

    options = ""

    if GetOption("abort"):
        options += " --abort"

    cmd = "%s %s" % (ut.path, options)

    run_ut = Command("test", None, cmd)

    Default(run_ut)
    Depends(run_ut, ut)
    AlwaysBuild(run_ut)


#-----------------------------------------------------------------------------
# produce unitest coverage

if do_coverage:

    base = os.path.splitext(ut.path)[0]

    targets = [
        base + '.gcda',
    ]

    # execution sequence
    actions = [
        'lcov --base-directory . --directory . --zerocounters -q',
        ut.path,
        'lcov --base-directory . --directory . -c -o tmp1.info',
        'lcov --remove tmp1.info "/usr*" -o tmp2.info',
        'lcov --remove tmp2.info "external/*" -o tmp3.info',
        'lcov --remove tmp3.info "numcpp/tests/*" -o coverage.info',
        'rm -rf coverage',
        'genhtml -o coverage -t "unittest coverage" --num-spaces 4 coverage.info',
        'echo Open coverage/index.html',
    ]

    run_ut = Command(
        target = targets,
        source = ut.path,
        action = actions,
    )

    Default(run_ut)

    Depends(run_ut, ut)

    AlwaysBuild(run_ut)
